I"<p>OK, so what is this <em>Infsabot</em>? Infsabot is a game I designed for people to write programs to play. Basically, it’s like chess, except where each piece acts independently of each other.</p>

<h2 id="the-game">The Game</h2>

<p>There is a square board where each square is either empty or contains material. The game pieces, robots, require material to stay alive and perform tasks. At each turn, every robot reports which of the following it wants to do:</p>

<ul>
  <li>Noop: The robot does nothing</li>
  <li>Die: The robot dies</li>
  <li>Dig: The robot digs to try to extract material</li>
  <li>Move: The robot can move in one of the four compass directions</li>
  <li>Fire: The robot can fire a quick-moving projectile (e.g., travel time = 0) in a compass direction</li>
  <li>Message: The robot can send a quick-moving message in a compass direction</li>
  <li>Spawn: The robot can create a new robot with its own initial material and appearance</li>
</ul>

<p>To decide what to do, a robot has access to two things:</p>

<ul>
  <li>A picture of the positions around it, to a given distance
    <ul>
      <li>Whether or not the spot contains material</li>
      <li>Whether or not the spot contains a robot, and it’s appearance (<em>not</em> it’s team)</li>
    </ul>
  </li>
  <li>A snapshot of its own hard drive, which is a massive relational database between Strings and Strings</li>
  <li>A list of all messages received</li>
</ul>

<!--end excerpt-->

<p>Crucially, there is no way to tell whether a robot one sees is on one’s own team or not. This must be accomplished through sending an receiving messages.</p>

<h2 id="robot-strategies">Robot strategies</h2>

<p>A robot strategy can be expressed as a type</p>

<pre><code class="language-haskell">type RobotProgramResult = (RobotAction, InternalState)
type RobotProgram = KnownState -&gt; RobotProgramResult
</code></pre>

<p>To create a baseline for human-written strategies, I am going to use an adaptive algorithm to try to generate a strategy. This basic algorithm won’t take into account any of the “human” factors like messages and internal state.</p>

<p>I will reify the concept of a (non-recursive) robot program by representing it as a long expression tree. The basic concept is something like this:</p>

<pre><code class="language-haskell">data ActionType = TDie | TNoop | TFire | TDig | TMoveIn | TSpawn

data RP =
    DefaultRepresentation ActionType |
    ModifyMaterial ExprInt RP |
        -- Applies to Fire, Spawn
    ModifyDirection ExprDir RP |
        -- Applies to Move, Spawn, Fire, Send
    ModifyAppearance ExprInt RP |
        -- Applies to Spawn. RGB treated as a single integer.
    IfRP ExprBool RP RP
</code></pre>

<p>So, <code>DefaultRepresentation</code> describes a default representation of the given <code>ActionType</code>. <code>Modify*</code> modifies the given field, if it exists, of the given action. The various <code>Expr*</code> types represent expression tree types, given below.</p>

<pre><code class="language-haskell">data ExprInt =
    ConstInt Int |
    RobAppear ExprPath ExprInt | -- Coordinate; Default if no robot
    MaterialLevel | -- Current material level
    Age | -- Current Age
    (:*) ExprInt ExprInt |
    (:+) ExprInt ExprInt |
    (:-) ExprInt ExprInt |
    (:/) ExprInt ExprInt |
    Mod ExprInt ExprInt |
    IfInt ExprBool ExprInt ExprInt |
    SwitchInt ExprDir ExprInt ExprInt ExprInt ExprInt

-- Basically [ExprDir]
    -- Written like this to make the tree more explicit
data ExprPath =
    Here |
    Offset ExprDir ExprPath

data ExprDir =
    ConstDir RDirection |
    IfDir ExprBool ExprDir ExprDir

data ExprBool =
    ConstBool Bool |
    CanSee ExprPath |
    MaterialAt ExprPath | -- False if can't see that far
    RobotAt ExprPath | -- False if can't see that far
    EqualInt ExprInt ExprInt |
    GreaterInt ExprInt ExprInt |
    EqualDir RDirection RDirection |
    Not ExprBool |
    (:&amp;) ExprBool ExprBool |
    (:|) ExprBool ExprBool
</code></pre>

<p>OK, so that’s a lot of mutually recursive trees. I’m going to skip the boring implementation of <code>eval</code> for each of these trees, but here’s a simple interface for that purpose:</p>

<pre><code class="language-haskell">class Evaluable given from to where
    eval :: given -&gt; from -&gt; to

instance Evaluable KnownState ExprInt Ratio where
    -- Evaluates to a Ratio, or fraction, for precision reasons.
    -- Round to get an actual integer
    ...
instance Evaluable KnownState ExprBool Bool where
    ...
instance Evaluable KnownState ExprDir RDirection where
    ...
instance Evaluable KnownState ExprPath [RDirection] where
    ...
</code></pre>

<h2 id="constructing-trees">Constructing Trees</h2>

<p>OK, so to construct some trees. Here’s a simple move and dig strategy that tries to maximize the digginess of a given robot.</p>

<pre><code class="language-haskell">digStrategy
    = IfRP (MaterialAt Here)
        (DefaultRepresentation TDig)
        (ModifyDirection
            (IfDir
                (MaterialAt
                    (Offset (ConstDir N) Here))
                (ConstDir N)
                (IfDir
                    (MaterialAt
                        (Offset (ConstDir E) Here))
                    (ConstDir E)
                    (IfDir
                        (MaterialAt
                            (Offset (ConstDir S) Here))
                        (ConstDir S)
                        (ConstDir W))))
            (DefaultRepresentation TMoveIn))
</code></pre>

<p>That’s some damn good looking Scheme—ahem, Infsabot Strategy DSL!</p>

<p>Anyway, next time I’ll cover how to generate code in the form of this structure. Hopefully, the computer can do most of the heavy lifting!</p>

<p>Note: I made a few corrections to <code>digStrategy</code> because it wasn’t exactly compiling.</p>
:ET